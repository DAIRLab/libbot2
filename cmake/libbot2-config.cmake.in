#[=============================================================================[
This file is part of @PROJECT_NAME@.

@PROJECT_NAME@ is free software: you can redistribute it and/or modify it
under the terms of the GNU Lesser General Public License as published by the
Free Software Foundation, either version 3 of the License, or (at your
option) any later version.

@PROJECT_NAME@ is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
or FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public
License for more details.

You should have received a copy of the GNU Lesser General Public License
along with @PROJECT_NAME@. If not, see <https://www.gnu.org/licenses/>.
#]=============================================================================]

@PACKAGE_INIT@

if(CMAKE_VERSION VERSION_LESS 3.1)
  message(FATAL_ERROR
    "CMake 3.1 or higher is required to use @PROJECT_NAME@. "
    "You are running version ${CMAKE_VERSION}."
  )
endif()

cmake_policy(PUSH)
cmake_policy(VERSION 3.1)
set(CMAKE_IMPORT_FILE_VERSION 1)

if(CMAKE_VERSION VERSION_LESS 3.9)
  # Imported from CMake 3.9.0
  macro(find_dependency dep)
    if (NOT ${dep}_FOUND)
      set(cmake_fd_quiet_arg)
      if(${CMAKE_FIND_PACKAGE_NAME}_FIND_QUIETLY)
        set(cmake_fd_quiet_arg QUIET)
      endif()
      set(cmake_fd_required_arg)
      if(${CMAKE_FIND_PACKAGE_NAME}_FIND_REQUIRED)
        set(cmake_fd_required_arg REQUIRED)
      endif()

      get_property(cmake_fd_alreadyTransitive GLOBAL PROPERTY
        _CMAKE_${dep}_TRANSITIVE_DEPENDENCY
      )

      find_package(${dep} ${ARGN}
        ${cmake_fd_quiet_arg}
        ${cmake_fd_required_arg}
      )

      if(NOT DEFINED cmake_fd_alreadyTransitive OR cmake_fd_alreadyTransitive)
        set_property(GLOBAL PROPERTY _CMAKE_${dep}_TRANSITIVE_DEPENDENCY TRUE)
      endif()

      if (NOT ${dep}_FOUND)
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "${CMAKE_FIND_PACKAGE_NAME} could not be found because dependency ${dep} could not be found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND False)
        return()
      endif()
      set(cmake_fd_required_arg)
      set(cmake_fd_quiet_arg)
      set(cmake_fd_exact_arg)
    endif()
  endmacro()
else()
  include(CMakeFindDependencyMacro)
endif()

set(WITH_BOT_FRAMES @WITH_BOT_FRAMES@)
set(WITH_BOT_LCM_UTILS @WITH_BOT_LCM_UTILS@)
set(WITH_BOT_LCMGL @WITH_BOT_LCMGL@)
set(WITH_BOT_PARAM @WITH_BOT_PARAM@)
set(WITH_BOT_PROCMAN @WITH_BOT_PROCMAN@)
set(WITH_BOT_VIS @WITH_BOT_VIS@)

find_dependency(bot2-core CONFIG
  HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-core"
)

if(WITH_BOT_LCM_UTILS)
  find_dependency(bot2-lcm-utils CONFIG
    HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-lcm-utils"
  )
endif()

if(WITH_BOT_PARAM)
  find_dependency(bot2-param CONFIG
    HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-param"
  )
endif()

if(WITH_BOT_PROCMAN)
  find_dependency(bot2-procman CONFIG
    HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-procman"
  )
endif()

if(WITH_BOT_LCMGL)
  find_dependency(bot2-lcmgl CONFIG
    HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-lcmgl"
  )
endif()

if(WITH_BOT_FRAMES AND WITH_BOT_LCMGL AND WITH_BOT_PARAM)
  find_dependency(bot2-frames CONFIG
    HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-frames"
  )
endif()

if(WITH_BOT_VIS)
  find_dependency(bot2-vis CONFIG
    HINTS "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bot2-vis"
  )
endif()

check_required_components(@PROJECT_NAME@)

unset(CMAKE_IMPORT_FILE_VERSION)
cmake_policy(POP)
