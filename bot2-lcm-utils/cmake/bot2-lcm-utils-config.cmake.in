#[=============================================================================[
This file is part of @PROJECT_NAME@.

@PROJECT_NAME@ is free software: you can redistribute it and/or modify it
under the terms of the GNU Lesser General Public License as published by the
Free Software Foundation, either version 3 of the License, or (at your
option) any later version.

@PROJECT_NAME@ is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
or FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public
License for more details.

You should have received a copy of the GNU Lesser General Public License
along with @PROJECT_NAME@. If not, see <https://www.gnu.org/licenses/>.
#]=============================================================================]

@PACKAGE_INIT@

if(CMAKE_VERSION VERSION_LESS 3.1)
  message(FATAL_ERROR
    "CMake 3.1 or higher is required to use @PROJECT_NAME@. "
    "You are running version ${CMAKE_VERSION}."
  )
endif()

cmake_policy(PUSH)
cmake_policy(VERSION 3.1)
set(CMAKE_IMPORT_FILE_VERSION 1)

if(CMAKE_VERSION VERSION_LESS 3.9)
  # Imported from CMake 3.9.0
  macro(find_dependency dep)
    if (NOT ${dep}_FOUND)
      set(cmake_fd_quiet_arg)
      if(${CMAKE_FIND_PACKAGE_NAME}_FIND_QUIETLY)
        set(cmake_fd_quiet_arg QUIET)
      endif()
      set(cmake_fd_required_arg)
      if(${CMAKE_FIND_PACKAGE_NAME}_FIND_REQUIRED)
        set(cmake_fd_required_arg REQUIRED)
      endif()

      get_property(cmake_fd_alreadyTransitive GLOBAL PROPERTY
        _CMAKE_${dep}_TRANSITIVE_DEPENDENCY
      )

      find_package(${dep} ${ARGN}
        ${cmake_fd_quiet_arg}
        ${cmake_fd_required_arg}
      )

      if(NOT DEFINED cmake_fd_alreadyTransitive OR cmake_fd_alreadyTransitive)
        set_property(GLOBAL PROPERTY _CMAKE_${dep}_TRANSITIVE_DEPENDENCY TRUE)
      endif()

      if (NOT ${dep}_FOUND)
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "${CMAKE_FIND_PACKAGE_NAME} could not be found because dependency ${dep} could not be found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND False)
        return()
      endif()
      set(cmake_fd_required_arg)
      set(cmake_fd_quiet_arg)
      set(cmake_fd_exact_arg)
    endif()
  endmacro()
else()
  include(CMakeFindDependencyMacro)
endif()

find_dependency(lcm 1.4 CONFIG
  HINTS "${PACKAGE_PREFIX_DIR}/lib/cmake/lcm"
)

if(NOT TARGET @NAMESPACE@bot-spy)
  add_executable(@NAMESPACE@bot-spy IMPORTED)
  set_target_properties(@NAMESPACE@bot-spy PROPERTIES
    IMPORTED_LOCATION "@PACKAGE_RUNTIME_INSTALL_DIR@/bot-spy"
  )
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@EXPORT_FILE@")

if(NOT TARGET @NAMESPACE@lcmtypes_@PROJECT_NAME@-java)
  include("${CMAKE_CURRENT_LIST_DIR}/@JAVA_EXPORT_FILE@" OPTIONAL)
endif()

check_required_components(@PROJECT_NAME@)

unset(CMAKE_IMPORT_FILE_VERSION)
cmake_policy(POP)
